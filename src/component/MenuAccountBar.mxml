<?xml version="1.0" encoding="utf-8"?>
<s:Group xmlns:fx="http://ns.adobe.com/mxml/2009" 
		xmlns:s="library://ns.adobe.com/flex/spark"
		scaleX="0.50"
		scaleY="0.50"
		creationComplete="group1_creationCompleteHandler(event)">
	
	
	<fx:Script>
		<![CDATA[
			import com.greensock.TweenLite;
			import com.greensock.easing.Cubic;
			
			import event.NotifyEvent;
			import event.NotifyEventConstants;
			
			import mx.events.FlexEvent;
			
			// VARIABLES
			private const radToDeg:Number = 180 / Math.PI;
			private var isDraggingKnob:Boolean = false;
			private var oldAngle:Number;
			
			private var _angle:Number = 0;
			public function get angle():Number
			{ return _angle; }
			public function set angle(new_angle:Number):void
			{
				_angle = new_angle;
				
				if (this)
					TweenLite.to(this,0.5,{shortRotation:{rotation:this.angle},overwrite:true,ease:Cubic.easeOut});
			}
			
			private function stopDragging():void
			{
				isDraggingKnob = false;
				
				stage.removeEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				stage.removeEventListener(Event.DEACTIVATE, onDeactivate);
				stage.removeEventListener(Event.MOUSE_LEAVE, onDeactivate);
				
				stage.removeEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			protected function onMouseDown(event:MouseEvent):void
			{
				if (isDraggingKnob)
				{ stopDragging(); }
				isDraggingKnob = true;
				oldAngle = Math.atan2(this.mouseY, this.mouseX) * radToDeg;
				stage.addEventListener(MouseEvent.MOUSE_UP, onMouseUp);
				stage.addEventListener(Event.DEACTIVATE, onDeactivate);
				stage.addEventListener(Event.MOUSE_LEAVE, onDeactivate);
				
				stage.addEventListener(MouseEvent.MOUSE_MOVE, onMouseMove);
			}
			
			private function onMouseUp(mouseEv:MouseEvent):void
			{
				updateVolume(this.mouseX, this.mouseY);
				stopDragging();
			}
			
			private function onDeactivate(ev:Event):void
			{
				stopDragging();
			}
			
			private function onMouseMove(mouseEv:MouseEvent):void
			{ updateVolume(this.mouseX, this.mouseY); }
			
			private function updateVolume(newX:Number, newY:Number):void
			{
				if ((isDraggingKnob) && (this.enabled))
				{
					var newAngle:Number = Math.atan2(newY, newX) * radToDeg; 
					if ((oldAngle < -90) && (newAngle > 90))
					{ oldAngle += 360; }
					if ((oldAngle > 90) && (newAngle < -90))
					{ oldAngle -= 360; }
					var angleDiff:Number = newAngle - oldAngle;
					this.angle += angleDiff;
					oldAngle = newAngle;
				}
			}
			
			protected function group1_creationCompleteHandler(event:FlexEvent):void
			{
				this.addEventListener(MouseEvent.MOUSE_DOWN,onMouseDown);
			}
			
			protected function btnMyAccount_clickHandler(e:MouseEvent):void
			{
				this.dispatchEvent(new NotifyEvent(NotifyEventConstants.ACCOUNTCLICK,e.currentTarget.id));
			}
			
		]]>
	</fx:Script>
	
	
	<fx:Declarations>
		<!-- 将非可视元素（例如服务、值对象）放在此处 -->
	</fx:Declarations>
	<s:Group x="-215" y="-217">
		<s:BitmapImage id="bg_account" smooth="true" smoothingQuality="high"
					   source="@Embed('assets/main/bg_Menu_AccountBar.png')"/>
		<s:Button id="btnMyAccount" x="85" y="8" skinClass="skins.AccountAccountSkin" scaleX="0.9" scaleY="0.9" click="btnMyAccount_clickHandler(event)"/>
		<s:Button id="btnMyPhoto" x="262" y="132" skinClass="skins.AccountPhotoSkin" scaleX="0.9" scaleY="0.9"/>
		<s:Button id="btnMyReward" x="71" y="225" skinClass="skins.AccountRewardSkin" scaleX="0.9" scaleY="0.9"/>
	</s:Group>
</s:Group>
